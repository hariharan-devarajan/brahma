# This will create IMPORTED targets for brahma. The executables will be
# brahma::<exe-name>-bin (e.g., brahma::brahma-bin) and the library will
# be brahma::brahma.

include("${CMAKE_CURRENT_LIST_DIR}/brahma-config-version.cmake")

list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}")
list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}/modules")
list(APPEND CMAKE_MODULE_PATH "@EXTRA_CMAKE_MODULE_DIR@")

#include(GNUInstallDirs)
include(ExternalProject)
include(brahma-utils)
include(CMakePackageConfigHelpers)


set(BRAHMA_VERSION ${PACKAGE_VERSION})

# Record compiler information
set(BRAHMA_C_COMPILER "@CMAKE_C_COMPILER@")
set(BRAHMA_CXX_COMPILER "@CMAKE_CXX_COMPILER@")

set(BRAHMA_C_FLAGS "@CMAKE_C_FLAGS@")
set(BRAHMA_CXX_FLAGS "@CMAKE_CXX_FLAGS@")

set(BRAHMA_C_STANDARD "@CMAKE_C_STANDARD@")
set(BRAHMA_CXX_STANDARD "@CMAKE_CXX_STANDARD@")

set(CMAKE_C_STANDARD_REQUIRED TRUE)
set(CMAKE_CXX_STANDARD_REQUIRED TRUE)

# Record the various flags and switches accumlated in BRAHMA
set(BRAHMA_GNU_LINUX @BRAHMA_GNU_LINUX@)
set(BRAHMA_HAS_STD_FILESYSTEM @BRAHMA_HAS_STD_FILESYSTEM@)
set(BRAHMA_HAS_STD_FSTREAM_FD @BRAHMA_HAS_STD_FSTREAM_FD@)

# Setup dependencies


@PACKAGE_INIT@

# Now actually import the BRAHMA target
set(_TMP_INCLUDE_DIRS "@CMAKE_INCLUDE_OUTPUT_DIRECTORY@")
foreach (_DIR ${_TMP_INCLUDE_DIRS})
  set_and_check(_INCLUDE_DIR "${_DIR}")
  list(APPEND BRAHMA_INCLUDE_DIRS "${_INCLUDE_DIR}")
endforeach (_DIR "${_TMP_INCLUDE_DIRS}")

set(_TMP_LIBRARY_DIRS "@CMAKE_LIBRARY_OUTPUT_DIRECTORY@")
foreach (_DIR ${_TMP_LIBRARY_DIRS})
  set_and_check(_LIBRARY_DIR "${_DIR}")
  list(APPEND BRAHMA_LIBRARY_DIRS "${_LIBRARY_DIR}")
endforeach (_DIR ${_TMP_LIBRARY_DIRS})

if (NOT TARGET brahma::brahma)
  include(${CMAKE_CURRENT_LIST_DIR}/brahma-targets.cmake)
endif (NOT TARGET brahma::brahma)

find_package(gotcha 2.1.2 REQUIRED)
set(BRAHMA_INCLUDE_MPI @BRAHMA_BUILD_WITH_MPI@)
if (BRAHMA_INCLUDE_MPI)
  find_package(MPI COMPONENTS CXX REQUIRED)
endif()

check_required_components(brahma)

set(BRAHMA_LIBRARIES brahma)